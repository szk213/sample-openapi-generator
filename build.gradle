plugins {
	id 'org.springframework.boot' version '2.3.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'war'
	id "org.openapi.generator" version "5.1.0"
}


openApiGenerate {
    generatorName = "spring"
    configFile = "$rootDir/specs/config.json".toString()
    inputSpec = "$rootDir/specs/petstore-v3.0.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "org.openapi.example.api"
    invokerPackage = "org.openapi.example.invoker"
    modelPackage = "org.openapi.example.model"
    configOptions = [
        dateLibrary: "java8"
    ]
//    systemProperties = [
//        modelDocs: 'false'
//    ]
}
compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"

group = 'com.github.szk213.sample-openapi-generator'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	implementation group: 'io.springfox', name: 'springfox-spring-web', version: '3.0.0'
	
	implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
	implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.2'
	
	implementation 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}